plugins {
    id 'org.springframework.boot' version '2.3.0.M4'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    /* Referenced from https://github.com/spring-projects/spring-restdocs/tree/master/samples/web-test-client */
    /* for rest docs */
    id 'org.asciidoctor.jvm.convert' version '3.2.0'
    /*id 'org.asciidoctor.convert' version '2.4.0'*/
    /* generate image and then push it to DockerHub */
    id 'com.google.cloud.tools.jib' version '2.1.0'
}

group = 'team.star'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

/* ====== specify DockerHub repo By Google JIB framework ====== */
// if logged on your local docker, you don't need to configure the following two global variable
// Or, you need configure them in gradle.properties and use them as follow.
if (!hasProperty('DOCKER_HUB_USERNAME') || !hasProperty('DOCKER_HUB_PASSWORD')) {
    jib.to.image = 'purplemystic/blog'
} else {
    jib {
        container.format = 'OCI'
        from {
            // Retrieves the base image from the Docker daemon.
            // image = "docker://openjdk:${sourceCompatibility}"
            image = "openjdk:${sourceCompatibility}"
        }
        to {
            image = "${DOCKER_HUB_USERNAME}/blog"
            auth {
                username = DOCKER_HUB_USERNAME // Defined in 'gradle.properties'.
                password = DOCKER_HUB_PASSWORD
            }
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExtensions
}

repositories {
    mavenLocal()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/libs-snapshot' }
    mavenCentral()
}

/* for rest docs */
ext {
    set('snippetsDir', file('build/generated-snippets'))
    set('springRestdocsVersion', '2.0.4.RELEASE')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
    /* for rest docs */
    testImplementation "org.springframework.restdocs:spring-restdocs-webtestclient:${springRestdocsVersion}"
    asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor:${springRestdocsVersion}"
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    dependsOn test
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
